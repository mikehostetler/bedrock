defmodule Bedrock.DataPlane.Log.Shale.Facts do
  @moduledoc false
  alias Bedrock.DataPlane.Log
  alias Bedrock.DataPlane.Log.Shale.State

  @spec info(State.t(), Log.fact_name()) :: {:ok, term()} | {:error, :unsupported}
  @spec info(State.t(), [Log.fact_name()]) :: {:ok, %{Log.fact_name() => term()}}
  def info(%State{} = t, fact) when is_atom(fact) do
    case gather_info(fact, t) do
      {:error, _reason} = error -> error
      info -> {:ok, info}
    end
  end

  def info(%State{} = t, facts) when is_list(facts) do
    {:ok,
     Map.new(facts, fn
       fact_name -> {fact_name, gather_info(fact_name, t)}
     end)}
  end

  @spec supported_info() :: [Log.fact_name()]
  def supported_info,
    do: [
      :id,
      :kind,
      :minimum_durable_version,
      :available_after,
      :oldest_version,
      :last_version,
      :otp_name,
      :pid,
      :state,
      :supported_info
    ]

  @spec gather_info(Log.fact_name(), State.t()) ::
          String.t()
          | :log
          | atom()
          | pid()
          | [Log.fact_name()]
          | :unavailable
          | Bedrock.version()
          | {:error, :unsupported}
  # Worker facts
  defp gather_info(:id, %{id: id}), do: id
  defp gather_info(:kind, _t), do: :log
  defp gather_info(:otp_name, %State{otp_name: otp_name}), do: otp_name
  defp gather_info(:pid, _), do: self()
  defp gather_info(:supported_info, _), do: supported_info()

  # Transaction Log facts
  defp gather_info(:minimum_durable_version, %{min_durable_version: nil}), do: :unavailable
  defp gather_info(:minimum_durable_version, %{min_durable_version: v}), do: v

  defp gather_info(:available_after, t), do: t.available_after
  defp gather_info(:oldest_version, t), do: t.oldest_version
  defp gather_info(:last_version, t), do: t.last_version

  # Everything else...
  defp gather_info(_, _), do: {:error, :unsupported}
end
