defmodule Bedrock.DataPlane.Log.Telemetry do
  @moduledoc false
  alias Bedrock.DataPlane.Log
  alias Bedrock.DataPlane.Transaction
  alias Bedrock.Telemetry

  @spec trace_metadata() :: map()
  def trace_metadata, do: Process.get(:trace_metadata, %{})

  @spec trace_metadata(metadata :: map()) :: map()
  def trace_metadata(metadata), do: Process.put(:trace_metadata, Enum.into(metadata, trace_metadata()))

  @spec trace_started() :: :ok
  def trace_started, do: Telemetry.execute([:bedrock, :log, :started], %{}, trace_metadata())

  @spec trace_lock_for_recovery(epoch :: Bedrock.epoch()) :: :ok
  def trace_lock_for_recovery(epoch) do
    Telemetry.execute(
      [:bedrock, :log, :lock_for_recovery],
      %{},
      Map.put(trace_metadata(), :epoch, epoch)
    )
  end

  @spec trace_recover_from(
          source_logs :: [Log.ref()],
          replay_after :: Bedrock.version(),
          last_inclusive :: Bedrock.version()
        ) :: :ok
  def trace_recover_from(source_logs, replay_after, last_inclusive) do
    Telemetry.execute(
      [:bedrock, :log, :recover_from],
      %{},
      Map.merge(trace_metadata(), %{
        source_logs: source_logs,
        replay_after: replay_after,
        last_inclusive: last_inclusive
      })
    )
  end

  @spec trace_push_transaction(transaction :: Transaction.encoded()) :: :ok
  def trace_push_transaction(transaction) when is_binary(transaction) do
    Telemetry.execute(
      [:bedrock, :log, :push],
      %{},
      Map.put(trace_metadata(), :transaction, transaction)
    )
  end

  @spec trace_push_out_of_order(
          expected_version :: Bedrock.version(),
          current_version :: Bedrock.version()
        ) :: :ok
  def trace_push_out_of_order(expected_version, current_version) do
    Telemetry.execute(
      [:bedrock, :log, :push_out_of_order],
      %{},
      Map.merge(trace_metadata(), %{
        expected_version: expected_version,
        current_version: current_version
      })
    )
  end

  @spec trace_pull_transactions(from_version :: Bedrock.version(), opts :: Keyword.t()) :: :ok
  def trace_pull_transactions(from_version, opts) do
    Telemetry.execute(
      [:bedrock, :log, :pull],
      %{},
      Map.merge(trace_metadata(), %{
        from_version: from_version,
        opts: opts
      })
    )
  end

  @spec trace_trim(
          floor :: Bedrock.version(),
          last_version :: Bedrock.version(),
          lag_us :: non_neg_integer(),
          segments_recycled :: non_neg_integer(),
          segments_retained :: non_neg_integer()
        ) :: :ok
  def trace_trim(floor, last_version, lag_us, segments_recycled, segments_retained) do
    Telemetry.execute(
      [:bedrock, :log, :trim],
      %{
        lag_us: lag_us,
        segments_recycled: segments_recycled,
        segments_retained: segments_retained
      },
      Map.merge(trace_metadata(), %{
        floor: floor,
        last_version: last_version
      })
    )
  end

  @spec trace_floor_lag_alarm(
          floor :: Bedrock.version(),
          last_version :: Bedrock.version(),
          lag_us :: non_neg_integer(),
          limit_us :: non_neg_integer()
        ) :: :ok
  def trace_floor_lag_alarm(floor, last_version, lag_us, limit_us) do
    Telemetry.execute(
      [:bedrock, :log, :floor_lag_alarm],
      %{lag_us: lag_us, limit_us: limit_us},
      Map.merge(trace_metadata(), %{
        floor: floor,
        last_version: last_version
      })
    )
  end
end
